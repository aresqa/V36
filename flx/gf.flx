  gf.flx : Datei vom Webserver allegro-b.de holen und lokal speichern
  2016-02-15    Typen .flx, .inc, .rtf, .txt, .vw, .htm, .ap?
  

  War kein Name beim Aufruf angegeben?
if "" jump dnam
  den Namen in $dn kopieren
ins $dn
jump getit

  Dateinamen abfragen
:dnam
ask Dateiname?
if "" end
ins $dn

:getit
  Name des Ordners, je nach Dateityp
var $dn "xx"
if %~.flxxx% var P "flex\\"
if %~.incxx% var P "flex\\"
if %~.rtfxx% var P "help\\"
if %~.txtxx% var P "help\\"
if %~.vwxx% var P "help\\"
if %~.htmxx% var P "html\\"

  alle andern sollen ins ProgDir
if not %\\% var P

ins $fo

  Dateityp auswerten
var $dn (b".") "/"
if "/" jump notyp
if %pr/% var "exparam"
if %pt/% var "exptab"
if %pi/% var "inxparam"
if %im/% var "imparam"
if "cony" var "conf"
var (F"/")
ins $typ

jump getdat

:notyp
var $dn
if "uif" var "uif"
if "h" var "h"
ins $typ


:getdat
  Datei holen
var "http://www.allegro-b.de/files/" $typ "/" $dn
get I
if "" mes Kein Erfolg;end
if %404 Not Found% jump notf

:ersetz
  Ersetzung der Zeichenfolge 10 13 durch 13
ins _^J^M_^M_
ins $

  Datei schreiben
var $fo $dn
open x
wri $
close x

var "Datei " $dn " wurde im Ordner " $fo " abgelegt"
mes

yes Datei anzeigen?
if not yes end

var $dn
help
end

:notf
ins $
var $dn
if "gf.flx" var $;jump ersetz
var "Die Datei " $dn " wurde auf dem Server nicht gefunden"
mes

