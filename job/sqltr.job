// sqltr.job : Excel-Tabellendatei (CSV-Textform) lesen und wandeln
// 2016-04-11
// Stellen fuer Eingriffe markiert mit  ***


// Aufruf:   acon -jsqltr [-ff] [-ndateiname] [-Test]
// Optionen:
// -ff : nur Liste der Felder anzeigen
// -ndateiname
// -Test : Nur anzeigen, keine Datei schreiben


// Optionen
$OP.accept=-b -d -e -f -j -n -h -T

// Optionen holen aus der Aufrufzeile
perform OPGET   // in optsget.inc  
// optsget.inc  wertet die Optionen aus und macht aus -xabc  $OP_x abc

if $OP_h jump HELP

// Unbegrenzte zahl von Zeilen
set Z0

// Unicode-Umcodierung beim Dateilesen aus
set U2

// Exp.-Parameter, gebraucht wird nur Tabelle aw-ad: Windows -> DOS
exp p e-1
// *** hier ggls. andere Tabelle
exp t aw-ad

if $OP_n jump open

//Datei oeffnen
perf HELP
Wri "Dateiname? (CSV-Datei mit TAB als Separator)"
ask
if "" end
ins $OP_n

:open
var $OP_n
open 
if no jump nof

// erste Zeile lesen 
get
if can jump ende

var (f'"')

// TAB -> =!=
ins _^I_=!=_

ins #uex

// *** Ausgabedatei oeffnen: derselbe Dateiname mit .adt als Typ

var $OP_f
if not "f" jump vloop

// -ff : Datei f.d. Feldliste
var $OP_n (e".") ."fl"
ins $Outfile
open x
if no jump outfehl

// Zaehler f. Anzahl Felder
z=1

// Feldnamen in $tX ablegen
:floop

var "$t" z " " #uex(e"=!=" F'"')
ins

z+1
var #uex(b"=!=" f'[ "]') 
ins #uex
if #uex jump floop

var z
Z=

z=1

// Feldliste $tX fertig, ausgeben
:lp
Wri z " : "
wri z " : "
var "$t" z
var
ins $a
Wri $a n
wri $a n
z+1
eval Z -z
if >0 jump lp
close x
Wri n "Feldliste steht in " $Outfile n
end

// Jetzt die Datei abarbeiten
:vloop
// *** Ausgabedatei oeffnen: derselbe Dateiname mit .adt als Typ

var $OP_n (e".") ."adt"
ins $Outfile
open x
if no jump outfehl

Z=0
:vlo
z=1
// Zeile lesen
get
if can jump ende
var (f'"')

// TAB 
ins _^I_=!=_

// " im Feldtext
ins _""_"_


ins #uex

:pl

var #uex(e"=!=" F'"')
// umcodieren  *** ggfls. xco u  wenn UTF-Daten vorliegen
xco xp
ins $u
var "$f" z " " $u

ins
z+1

var #uex(b"=!=" f'[ "]') 
ins #uex
if #uex jump pl


// Testmodus?

if not $OP_T jump vl
// dann dieser output: (erste 5 Felder jedes Satzes)
Wri "f1=" $f1 n "f2=" $f2 n "f3=" $f3 n "f4=" $f4 n "f5=" $f5 " ... " n n
jump vlo

:vl
// *** Echtmodus: Auswertung
// *** Abschnitt zum Produzieren eines Datensatzes o.a. aus der gelesenen Zeile,
// Hier die konkrete Aktion, i.d.R. Produktion eines Datensatzes
// d.h. aus den Variablen  $fi (i=1,2,...)

// NUR ALS BEISPIEL fuer output im Externformat
wri "#00 " $f1 n
wri "#20 " $f5 n
wri "#40 " $f2 n
wri "#90 " $f15 " / " $f16 n
wri n

Z+1

Wri 13 Z

jump vlo

:ende
// Datei abschliessen
close x
Wri n n "Datei " $Outfile " ist erstellt" n

end

:nof
Wri "Keine Datei" n
end

:outfehl
Wri "Datei " $Outfile " kann nicht geoeffnet werden" n
end

// Routine zur Optionsauwertung hinzuladen (darin :OPGET)
include optsget.inc
end

:HELP
Wri "Aufruf:   acon -jsqltr [-ff] [-ndateiname]" n
Wri "OPTIONEN:" n
Wri "-ff   : nur Liste der Felder" n
Wri "-ndateiname : Auszuwertende CSV-Textdatei" n
Wri "-Test : Ausgabe der Daten auf Konsole statt Datei" n n
return


